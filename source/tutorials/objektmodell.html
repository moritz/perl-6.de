[[[menu main tut objekt]]]
[[[setvar title Perl 6 Objekte und Klassen]]]

<div id="content">
	<h1>Das Objektmodell von Perl 6</h1>

	<p>Um von dieser Seite profitieren zu können, sollten Sie  mit den
	Grundlagen von Perl 6 vertraut sein, und auch ein wenig von
	Objektorientierung verstehen - wenn Sie wissen, was eine Klasse, ein
	Objekt und Vererbung ist, reicht das vermutlich schon.</p>

	<p>Das Objektmodell von Perl 6 hat nur noch wenige Ähnlichkeiten mit
	dem von Perl 5, und muß jetzt den Vergleich mit "klassischen"
	objektorientierten Programmiersprachen nicht mehr scheuen.</p>

	<h2>Klassen definieren</h2>

	<p>Klassen werden mit dem <code>class</code>-Schlüsselwort definiert,
	das man auf zwei verschiedene Arten verwenden kann. Entweder als erste
	Anweisung in einer Datei in der Form <code>class MeineKlasse;</code>, dann
	ist der Rest der Datei die Definition der Klasse. Die zweite
	Möglichkeit sieht so aus:</p>

<pre>[[[syntax perl6]]]
class MeineKlasse {
	# hier kommt die Definition der Klasse
}
# hier ist die Definition der Klasse zu Ende
# und anderer Code kann hier stehen.
[[[endsyntax]]]</pre>

	<p>Objektvariablen (auch <em>Attribute</em> genannt) werden mit <code>has</code>
    anstatt <code>my</code> deklariert, und haben nach dem Sigil (also
    <code>$</code>, <code>@</code>, <code>%</code>) noch ein weiteres
    Sonderzeichen, ein Ausrufezeichen. Jedes Objekt einer Klasse hat seine
    eigene Kopie jeden Attributs. Methoden werden  mit <code>method</code> anstatt
	<code>sub</code> deklariert:</p>

<pre>[[[syntax perl6]]]
class Rechteck {
	has $!breite;
	has $!hoehe;

	method flaeche {
		return $!breite * $!hoehe;
	}
}

# benutze den Standardkonstruktor
my $r = Rechteck.new(:hoehe(5), :breite(3));

say $r.flaeche;     # 15

[[[endsyntax]]]</pre>

<p>Attribute sind privat, das heisst Code ausserhalb der Klasse hat keinen
Zugriff auf sie. Häufig ist das aber erwünscht, daher gibt es eine einfache
Syntax, um sie von aussen mithilfe einer Methode verfügbar zu machen:</p>

<pre>[% syntax perl6 %]
class Rechteck {
	has $.breite;
	has $.hoehe;

	method flaeche {
        # Die Namen der Attribute enthalten immer noch
        # das Ausrufezeichen
		return $!breite * $!hoehe;
	}
}
my Rechteck $r = Rechteck.new(:breite(5), :hoehe(3));
say $r.flaeche;         # 15
say $r.hoehe;           # 3
say $r.breite;          # 5
[% endsyntax %]</pre>

<p>Manchmal sollen die Attribute auch von aussen beschreibbar sein, dann fügt
man den Trait <code>is rw</code> an:</p>

<pre>[[[syntax perl6]]]
class Rechteck {
	has $.breite is rw;
	has $.hoehe is rw;

	method flaeche {
		return $.breite * $.hoehe;
	}
}
my Rechteck $r = Rechteck.new();
$r.breite = 2;
$r.hoehe = 5;
say $r.flaeche();
[[[endsyntax]]]</pre>

    <p>Private Methoden funktionieren mit der Syntax
    <code>method !meine_methode</code>, und können mit
    <code>self!meine_methode</code> aufgerufen werden.</p>

<pre>[% syntax perl6 %]
class Auto {
    has $!motor;
    has $!gang;

    method !gang_wechseln ($differenz) {
        $!gang += $differenz;
    }
    ...


    method beschleunigen {
        self!gang_wechseln(-1);
        # hier gas geben
        self!gang_wechseln(+1);
    }
}[% endsyntax %]</pre>


	<h2>Vererbung</h2>

	<p>Vererbung wird durch das <code>is</code>-Schlüsselwort
	deklariert:</p>

<pre>[[[syntax perl6]]]
class Form {
	...
}

class Rechteck is Form {
	...
}
[[[endsyntax]]]</pre>

	<p>Auch Mehrfachvererbung ist möglich:</p>

<pre>[[[syntax perl6]]]
class Kraftfahrzeug {
	...
}

class Wasserfahrzeug {
	...
}

class Amphibienfahrzeug is Kraftfahrzeug is Wasserfahrzeug {
	...
}
[[[endsyntax]]]</pre>
	<p>(Beispiel aus der <a
			href="http://de.wikipedia.org/wiki/Vererbung_(Programmierung)#Beispiel">Wikipedia</a>)</p>

	<p>Vererbung und andere <code>is ...</code>-Traits kann man auch
	innerhalb der Klasse schreiben:</p>
<pre>[[[syntax perl6]]]

class Amphibienfahrzeug {
	is Kraftfahrzeug;
	is Wasserfahrzeug;
	...
}
[[[endsyntax]]]</pre>

	<h2>Klassen erweitern</h2>

	<p>Wenn man eine Klasse erweitern will, ihr zum Beispiel eine Methode
	hinzufügen will, kann man das auch ohne Vererbung erreichen:</p>

<pre>[[[syntax perl6]]]
augment class String {
	method as_ebcdic {
		...
	}
}
[[[endsyntax]]]</pre>

	<p>Das hat den Vorteil, dass man auch fest eingebaute Datentypen wie
	String oder Int erweitern kann, und fremder Code, den man einbindet,
	verwendet automatisch die erweiterte Version. Allerdings können
    Erweiterungen in einem Teil eines Programms mit Erweiterungen in weit
    entfernten Programmteilen kollidieren - daher gelten sie als sehr
    schlechter Stil.</p>

	<p>Wenn man versucht, eine Klasse zu erweitern, ohne ein <code>is
	also</code> in die Deklaration zu schreiben, bekommt man eine
	Fehlermeldung, dass man eine Klasse redeklariert.</p>


	<h2>Roles</h2>

	<p>In Perl 6 sind Klassen vor allem dazu da, um "ist-ein"-Beziehungen
	für Typen zu deklarieren. Für die Wiederverwendbarkeit von Code sind
	<em>Roles</em>, auf Deutsch <em>Rolle</em> zuständig. Roles sind so
	etwas wie Interfaces in Java,
	die Implementierung der Methoden beinhalten.</p>

	<p>Genauer gesagt kann eine Role alles enthalten, was eine Klasse auch
	enthalten kann, nur kann man keine Objekte einer Role erstellen.</p>
<pre>[[[syntax perl6]]]
role Haustier {
	has $.halsband;
	method halsband_loesen {
		undefine $.halsband;
	}
	...
}

class Hund is Saeugetier does Haustier {
	...
}
[[[endsyntax]]]</pre>
	<p>Wie das Beispiel zeigt, inkorporiert eine Klasse eine Rolle mit dem
	Schlüsselwort <code>does</code>.</p>
