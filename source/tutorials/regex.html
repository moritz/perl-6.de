[[[menu main tut regex]]]
[[[setvar title Regular Expressions und Rules]]]

<div id="content">
    <h1>Regular Expressions und Rules</h1>

    <p>Regular Expressions, kurz Regexes, sind Muster, die Text
    beschreiben. Wenn man sie auf einen String "anwendet", überprüft man,
    ob der String auf zu dem Muster passt.</p>

<pre>[[[syntax perl6]]]
if "Mein Name ist Hase" ~~ m/Hase/ {
    say "In dem Text kam 'Hase' vor";
}
[[[endsyntax]]]</pre>

    <p>In diesem Beispiel wird ein Text darauf überprüft, ob der String
    "Hase" darin vorkommt. Dabei ist "Hase" die regex, und wird mit
    <code>m/.../</code> begrenzt.</p>

    <p>Die Regex wird mit dem <em>Smart Match Operator</em>
    <code>~~</code> auf den String angewandt, oder gematched wie man
    sagt.</p>

    <h2>Aufbau von Regexes</h2>

    <p>Im einfachsten Fall bestehen Regexes einfach auch Text, ein
    Buchstabe nach dem anderen.</p>

    <p>Dabei sollte man beachten, dass Leerzeichen keinerlei Bedeutung
    haben, d.h. <code>m/Hase/</code> und <code>m/Ha se/</code> haben die
    gleiche Bedeutung.</p>

    <p>Zusätlich gibt es Zeichen, die eine Sonderfunktion haben, zum Beispiel 
    der Punkt <code>.</code> steht für ein beliebiges Zeichen:</p>

<pre>[[[syntax perl6]]]
my $str = "Hosenträger";
if $str ~~ m/H.se/ {
    # passt auch.
}
if "Haase" ~~ m/H.se/ {
    # passt nicht, der Punkt steht für genau ein Zeichen
}
[[[endsyntax]]]</pre>

    <p>Es gibt auch Zeichenkombinationen, die etwas besonderes bedeuten,
    z.B. <code>\d</code> steht für eine Ziffer (Englisch digit =
    Ziffer).</p>

<pre>[[[syntax perl6]]]
my $zeit = "6H30";
if $zeit ~~ m/\d H \d\d/ {
    say "gültige Weckzeit";
} else {
    say "Damit kann ich nichts anfangen";
}
[[[endsyntax]]]</pre>
    <p>Die folgende Tabelle fasst die wichtigsten dieser
    Zeichenkombinationen zusammen. Dabei gilt, dass jeweils genau ein
    Zeichen gematched wird.</p>

    <p>Die Negation ist die Verneinung, d.h. wenn <code>\d</code> auf eine
    Ziffer passt, passt <code>\D</code> auf ein beliebiges Zeichen, das keine
    Ziffer ist.</p>

<table summary="Regex-Sonderzeichen 1">
    <tr>
        <th>Zeichen</th>
        <th>Bedeutung</th>
        <th>Beispiel</th>
        <th>Herkunft</th>
        <th>Negation</th>
    </tr>
    <tr>
        <td>.</td>
        <td>beliebiges Zeichen</td>
        <td>/</td>
        <td>...</td>
        <td></td>
    </tr>
    <tr>
        <td>\d</td>
        <td>Ziffer</td>
        <td>7</td>
        <td>digit = Ziffer</td>
        <td>\D</td>
    </tr>
    <tr>
        <td>\t</td>
        <td>Tabulator</td>
        <td>    </td>
        <td>tab = Tabulator</td>
        <td>\T</td>
    </tr>
    <tr>
        <td>\n</td>
        <td>Zeilenumbruch</td>
        <td>    </td>
        <td>newline = neue Zeile</td>
        <td>\N</td>
    </tr>
    <tr>
        <td>\w</td>
        <td>Buchstabe, Ziffer oder Unterstrich</td>
        <td>H</td>
        <td>word = Wort</td>
        <td>\W</td>
    </tr>
    <tr>
        <td>\s</td>
        <td>(Unicode)-Leerzeichen</td>
        <td> </td>
        <td>space = Leerzeichen</td>
        <td>\S</td>
    </tr>
</table>

    <h2>Alternativen</h2>

    <p>Alternativen sind <em>oder</em>-Verknüpfungen, das heißt die Regex
    matcht, wenn eine von mehreren Alternativen passt:</p>

<pre>[[[syntax perl6]]]
if $str ~~ m/der|die|das/ {
    say "Bestimmten Artikel gefunden";
}
[[[endsyntax]]]</pre>

    <p>Wenn sich die Alternative nicht über die gesamte Regex erstrecken
    soll, kann man ihren Gültigkeitsbereich mit eckigen Klammern
    einschränken:</p>

<pre>[[[syntax perl6]]]
m/ [ der | die | das ] \s Artikel / 
# passt auf "der Artikel", "die Artikel" oder "das Artikel"
[[[endsyntax]]]</pre>
    
    <p>Nur zur Erinnerung: Leerzeichen in Regexes werden ignoriert, wenn
    man Leerzeichen matchen will, muss man das zum Beispiel mit 
    <code>\s</code> angeben.</p>


    <h2>Quantoren, oder: "Wie oft darf's denn sein?"</h2>

    <p>Mit sogenannten Quantoren kann man angeben, wie häufig ein
    bestimmtes Zeichen oder Teilmuster vorkommen darf.</p>

    <p>Dabei steht ein Fragezeichen für "0 mal oder einmal":</p>

<pre>[[[syntax perl6]]]
m/abc?d/        # passt auf abcd oder abd
m/a[bc]?d/      # passt auf abcd oder ad
[[[endsyntax]]]</pre>
    <p>Ein <code>+</code> steht für "Ein mal oder beliebig oft":</p>

<pre>[[[syntax perl6]]]
m/abc+d/        # passt auf abcd, abccd, abcccd, ...
m/a[bc]+d/      # passt auf abcd, abcbcd, abcbcbcd, ...

if $str ~~ m/\d+/ {
    say "in $str kommt eine Zahl vor";
}
[[[endsyntax]]]</pre>

    <p>Ein <code>*</code> passt auf keine, eine oder beliebig
    viele Wiederholungen:</p>

<pre>[[[syntax perl6]]]
m/abc*d/        # passt auf abd, abcd, abccd, abcccd, ...
m/a[bc]*d/      # passt auf ab, abcd, abcbcd, abcbcbcd, ...
[[[endsyntax]]]</pre>

    <p>Wenn man z.B. eine Wiederholung genau vier mal haben will, kann man
    das mit der <code>**{4}</code>-Notation angeben:</p>

<pre>[[[syntax perl6]]]
m/ab**{4}c/     # passt auf abbbbc
m/ab**{2..4}c/  # passt auf abbc oder abbbc oder abbbbc
m/ab**{2..*}c/  # passt auf abbc, abbbc, abbbbc, abbbbbc, ...
[[[endsyntax]]]</pre>

    <h2>Regexes sinnvoll einsetzen</h2>

    <p>Bisher haben Sie gesehen, wie man mit Regexes überprüft, ob ein
    Muster in einem String vorkommt oder nicht.</p>

    <p>Natürlich kann man auch abfragen, welcher Teil eines Strings auf
    eine Regex passt:</p>

<pre>[[[syntax perl6]]]
my $str = "Du schuldest mir 122 Euro";

if $str ~~ m/\d+/ {
    say "$/ Euro, verstanden?";
}
[[[endsyntax]]]</pre>

    <p>Das Ergebnis des letzten Matches wird in der speziellen Variable
    <code>$/</code> gespeichert, und im String-<a
    href="kontext">Kontext</a> wird daraus automatisch der Teil des
    Strings, auf den die Regex gepasst hat.</p>

    <p>Man kann das Ergebnis auch explizit einer Variable zuweisen, und
    auf weitere Eigenschaften zugreifen:</p>

<pre>[[[syntax perl6]]]
my $match = "foobar" ~~ m/o+/;
say "Anfangsposition des Matches: ",    $match.from;
say "Endposition des Matches: ",        $match.to;
[[[endsyntax]]]</pre>

    <p>Mit den Funktionen <code>comb</code> und <code>split</code> kann
    man Strings in Arrays zerlegen.</p>

    <p><code>comb</code> liefert dabei alle Treffer einer Regex
    zurück:</p>

<pre>[[[syntax perl6]]]
my $text = "20 mal 2 ist 40";

my @zahlen = $text.comb(m/\d+/);
# Ergebnis: @zahlen = 20, 2, 40;
[[[endsyntax]]]</pre>

    <p>(Der Name kommt von <em>to comb</em>, was auch "etwas durchkämmen" im
    Sinne von "systematisch Durchsuchen" heißt.)</p>

    <p><code>split</code> trennt einen String mithilfe einer Regex, wobei
    der String überall dort getrennt wird, wo die Regex passt. Der
    passende Teil wird nicht zurückgeliefert:</p>

<pre>[[[syntax perl6]]]
my $text = "20 mal 2 ist 40";

my @worte = $text.split(m/\s+/);
# Ergebnis: @worte = "20", "mal", "2", "ist", "40";
[[[endsyntax]]]</pre>

    <p>Wenn man in einer Regex einen Schrägstrich <code>/</code>
    benutzt, muss man ihn mit einem Rückstrich escapen: <code>\/</code>.
    Eine andere Möglichkeit ist, ein anderes Zeichen als Begrenzung der
    Regex zu verwenden:</p>
<pre>[[[syntax perl6]]]
my $cmd = "/usr/bin/perl -w";
if $cmd ~~ m{^(/.*/)perl} {
    say "Perl liegt im Verzeichnis $/[0]";
}
[[[endsyntax]]]</pre>

    <p>Man kann fast beliebige Sonderzeichen zur Begrenzung verwenden,
    wenn man Klammern verwendet wird die Regex mit der entsprechenden
    schliessenden Klammer beendet.</p>

    <h2>Gierige und bescheidene Quantoren</h2>

    <p>Angenommen, man lässt die Regex <code>m/a.*b/</code> auf den String
    <code>afffbffb</code> los. Die meisten Anfänger erwarten, dass die
    Regex auf "afffb" passt, aber wenn man sich <code>$/</code>
    ausgeben lässt, erlebt man eine Überrasschung: Der ganze String passt
    auf die Regex.</p>

    <p>Der Grund dafür ist, dass die Quantoren <code>+</code>,
    <code>*</code>, <code>?</code> und <code>**{...}</code>
    <strong>gierig</strong>, englisch <em>greedy</em> sind, d.h. sie 
    passen immer auf einen möglichst langen String.</p>

    <p>Wenn der Programmteil von Perl, der die Regexes verarbeitet, auf
    eine Konstruktion wie <code>m/a.*c/</code> trifft, sucht er nach dem
    ersten "a", geht dann ans Ende des Strings, und sucht dann rückwärts
    nach dem ersten "c", wenn er das gefunden hat, ist er fertig.</p>

    <p>Um das zu verhindern, und dafür zu sorgen, dass ein möglichst
    kurzer String gefunden wird, kann man hinter jeden Quantoren ein
    Fragezeichen <code>?</code> stellen:</p>

<pre>[[[syntax perl6]]]
$m = "affbeeb" ~~ m/a .* b/;
say $m;             # Ausgabe: affbeeb
$m = "affbeeb" ~~ m/a .*? b/;
say $m;             # Ausgabe: affb
[[[endsyntax]]]</pre>

    <p>Genauso kann man aus <code>+</code> mit der Schreibweise
    <code>+?</code> "einen oder mehr, aber möglichst wenig davon" machen,
    aus <code>**{2...6}</code> mit <code>**{2..6}?</code> "zwei bis sechs
    Wiederholungen, aber möglichst wenig davon" usw.</p>

    <p>Man nennt diese Quantoren dann <em>non-greedy</em>, also
    nicht gierig.</p>


    <h2>Anker</h2>

    <p>Die bisherigen Regexes haben immer nur überprüft, ob ein bestimmtes
    Must in einem String vorkommt, aber nicht, ob der gesamte String zu
    dem Muster passt.</p>

    <p>Das geht, indem man die Regex <em>verankert</em>. Dazu gibt es die
    Sonderzeichen <code>^</code> und <code>$</code>, die auf Anfang bzw.
    Ende des Strings passen. Wenn man also überprüfen will, ob ein String
    mit einer Ziffer anfängt, geht das <code>m/^\d/</code>, und wenn man
    überprüfen will, ob ein String eine positive, ganze Zahl ist, geht das
    mit <code>m/^\d+$</code>.</p>

    <p>Dabei sollte man beachten, dass <code>^</code> und <code>$</code>
    keine Zeichen "aufbrauchen", d.h. <code>^a$</code> passt genau auf den
    String <code>"a"</code>.</p>

    <p>Wenn man sich nicht für Anfang und Ende eines Strings interessiert,
    sondern für Zeilenanfang und -Ende, so kann man <code>^^</code> und
    <code>$$</code> als Anker verwenden.</p>

<pre>[[[syntax perl6]]]
my $s = "He shouted\nRUN\nAnd I ran";

if $s ~~ m/ ^^ \w+ \n $$ / {
    # passt auf eine Zeile, die aus nur einem Wort  
    # und einem newline besteht, matched also
    say $/;
    # Ausgabe: RUN
}

if $s ~~ m/ ^ \w+ $ / {
    # passt nicht, da der String nicht nur 
    # aus einem Wort besteht.
}
[[[endsyntax]]]</pre>

    <p>Dabei gelten Anfang und Ende eines Strings auch immer als logischer
    Zeilenanfang bzw. -Ende.</p>


    <h2>Captures</h2>

    <p>Mit den bisher vorgestellten Elementen von Regexes ist es nicht
    möglich, nach einem Muster wie <em>Ein Wort, gefolgt von einem
    Leerzeichen, gefolgt von dem selben Wort</em> zu suchen.</p>

    <p>Das ändert sich mit sogenannten <em>captures</em> (auf Deutsch auch
    manchmal <em>fangende Klammern</em> genannt), die mit runden
    Klammern eingerichtet werden:</p>

<pre>[[[syntax perl6]]]
m/ (\w+) \s $0 /
[[[endsyntax]]]</pre>

    <p>Dabei wird der Match des in den runden Klammern stehenden Teils der
    Regex in der Variable <code>$0</code> gespeichert.</p>

    <p>Der String <code>"foo foo"</code> würde z.B. zu der Regex passen,
    nicht aber <code>"foo bar"</code>.</p>

    <p>Wenn es mehrere <em>Capturing Groups</em> gibt, wird das Ergebnis
    der ersten in <code>$0</code> abgespeichert, der zweiten in
    <code>$1</code> usw.</p>

    <p>Außerdem kann man das Match-Objekt, das beim Anwenden einer Regex
    entsteht, als Array verwenden, das als Elemente die verschiedenen
    Capturing Groups hat.</p>

<pre>[[[syntax perl6]]]
my @a = "der Artikel, das Adverb",
        "das Schiff, das Monster",
        "der Berg, das Schaf";

for @a -> $s {
    if $s ~~ m/(\w+) \s (\w+), \s  $0 \s (\w+)/ {
        say "$/[1] und $/[2] haben den gleichen Artikel: $/[0]";
    }
}
# Ausgabe:
# Schiff und Monster haben den gleichen Artikel: das
[[[endsyntax]]]</pre>

    <p>Dieses Muster sucht nach einem Wort <code>\w+</code> und speichert
    das Ergebnis in der Variablen <code>$0</code>, sucht weiter nach einem 
    Leerzeichen <code>\s</code>, nach noch einem Wort, das in <code>$1</code> 
    gespeichert wird, einem Komma, einem Leerzeichen, dem zuerst gefunden 
    Wort, und dann noch einem Wort, das in <code>$2</code> gespeichert wird.</p>

    <p>Außerhalb der Regex kann auf <code>$0</code> mit <code>$/[0]</code>
    oder mit <code>$0</code> zugegriffen werden, auf <code>$1</code> auch mit
    <code>$/[1]</code> usw.</p>

    <p>Ab dem 11. Capture mit Nummer 10 kann nur noch mit der
    <code>$/[$index]</code>-Notation darauf zugegriffen werden.</p>

    <p>Wenn man z.B. eine Datei einliest, kann man mit der folgenden Regex
    nach Strings suchen, die entweder mit <code>"</code> oder
    <code>'</code> begrenzt sind:</p>

<pre>[[[syntax perl6]]]
m/ ('|") .*? $0 /
[[[endsyntax]]]</pre>

    <p>Das sucht nach einem einfach oder doppelten Anführungszeichen, dann
    beliebig viele (aber möglichst wenige) Zeichen, und dann noch einmal
    dem Anfangszeichen.</p>

    <h2>Modifier</h2>

    <p>Mit sogenannten <em>Modifiern</em> kann man das Verhalten von Regexes
    auf verschiedene Arten verändern.</p> 

    <h3>Groß- und Kleinschreibung ignorieren - <code>:ignorecase</code></h3>

    <p>Mit dem <code>:i</code> oder <code>:ignorecase</code>-Modifier wird
    Groß- und Kleinschreibung ignoriert. Das gilt nicht nur für latinische
    Zeichen, sondern für beliebige Schriften, die das Konzept von Groß- und
    Kleinschreibung kennen.</p>

<pre>[% syntax perl6 %]
my $str = "Ab";
$str ~~ m:i/ab/;        # matched
regex foo {
    :ignorecase         # kann auch innerhalb einer Regex stehen
    ab                  # passt auf ab, Ab, aB und AB
}
[% endsyntax %]</pre>

    <h3>Akzente ignorieren - <code>:basechar</code></h3>

    <p>Mit <code>:basechar</code> oder <code>:b</code> werden alle Arten von
    Akzente von der Regex ignoriert.</p>

    <p><code>m:b/a/</code> matched also nicht nur "a", sondern auch "ä",
    "à", "á", "â", "ã", "ä" oder "å".</p>

    <h3>Alle Treffer finden - <code>:global</code></h3>

    <p>Mit dem <code>:g</code> oder <code>:global</code>-Modifier kann man
    nach allen, nichtüberlappenden Treffern einer Regex suchen:</p>

<pre>[% syntax perl6 %]
my $str = "3 = (4 - 2) + 1";
my @zahlen = $str ~~m:g/\d+/;
say @zahlen.join(", ");         # 3, 4, 2, 1
[% endsyntax %]</pre>

    <h3>Überlappende Treffer finden - <code>:overlap</code></h3>

    <p>Mit dem <code>:overlap</code> oder <code>:ov</code>-Modifier findet 
    man auch überlappende Treffer, aber pro Anfangsposition nur eines:</p>
<pre>[% syntax perl6 %]
my @treffer = 'abababa' ~~ m:ov/a.+a/;
# Ergebnis: 'abababa', 'ababa', 'aba'
[% endsyntax %]</pre>

    <h3>Wirklich alle Treffer finden - <code>:exhaustive</code></h3>

    <p>Mit dem <code>:exhaustive</code> oder <code>:ex</code>-Modifier kann
    man jedem möglichen Treffer suchen lassen:</p>

<pre>[% syntax perl6 %]
$str = "abracadabra";

for $str ~~ m:exhaustive/ a (.*?) a / -> $match {
    say $match[0];  # br brac bracad bracadabr c cad cadabr d dabr br
}
[% endsyntax %]</pre>

    <h3 id="sigspace-modifer">:sigspace</h3>

    <p>Mit dem <code>:sigspace</code> oder <code>:s</code>-Modifier werden
    alle Leerzeichen so interpreteirt, als stünde an ihrere Stelle die Regex
    <code>&lt;.ws&gt;</code>.</p>

    <p>Die matcht, wenn sie zwischen zwei Wörtern steht, <code>\s+</code>, und
    sonst <code>\s*</code>.</p>

    <p>(Der Punkt am Anfang bewirkt, dass keine <a href="#named_capture">Named
    Capture</a> erstellt wird.)</p>

<pre>[[[syntax perl6]]]
m:sigspace/ \d+ [\+ | \*] \d+ /
# matcht: '2+3', '2 + 3', ' 2 + 3 ', ... 
[[[endsyntax]]]</pre>

    <h3 id="ratchet-modifier">Backtracking verhindern -
    <code>:ratchet</code></h3>

    <p>Bisher haben wir so getan, also ob die Regex-Engine auf magische Weise
    wüßte, ob eine Regex zu einem String passt oder nicht.</p>
    <p>Das ist natürlich nicht der Fall, sie muss in dem String suchen.</p>
    <p>Dabei gibt es häufig anfangs mehrere Möglichkeiten, wie die
    Regex-Engine einen String matchen könnte.</p>

    <p>Ein Beispiel: Wenn die Regex <code>\w+\d+</code> in dem String
    <code>"ab123"</code> das erste Mal auf eine Ziffer trifft, könnte das 
    immer noch zu <code>\w+</code> gehören, oder
    aber schon zu <code>\d+</code>. Da die Engine das noch nicht entscheiden
    kann, ohne den Rest des Strings zu kennen, merkt sie sich, dass es an 
    dieser Stelle noch eine andere Möglichkeit gegeben hätte, und fährt damit
    fort, <code>\w+</code> zu matchen.</p>

    <p>Bei der nächsten Ziffer, <code>2</code> passiert genau das Gleiche,
    ebenso bei der dritten.</p>

    <p>Dann ist die Engine am Ende des Strings angelangt, die Regex ist aber
    noch nicht zuende. Also geht die Regex Engine zu dem letzten Punkt zurück,
    an dem es mehrere Möglichkeiten gab, das war beim letzten Zeichen, der
    <code>3</code>.</p>

    <p>Dort wird versucht, die verbleibende Möglichkeit, <code>\d+</code>, zu
    matchen. Das ist erfolgreich, also matcht die ganze Regex.</p>

    <p>Dieser Vorgang des Speicherns von mehrdeutigen Stellen und wieder
    darauf zurückzugehen, wenn auf keine andere Art gematcht werden kann,
    nennt sich <em>Backtracking</em>.</p>

    <p>Manchmal möchte man dieses Verhalten jedoch abschalten, meistens um das
    Matchen einer Regex zu beschleunigen.</p>

    <p>Das geht mit dem <code>:ratchet</code>-Modifier.</p>

    <p>Die Idee dahinter ist, dass man häufig genauer als die Regex-Engine
    weiß, auf was eine Regex matchen soll und auf was nicht.</p>

    <p>Wenn man z.B. eine Datei der Form</p>

<pre>
name1 = wert1
name2 = wert2
</pre>

    <p>parsen will, nimmt eine Regex der Form <code>^^(\w+) \s* \= \s*
    (\N+)\n$$</code> - und man weiss genau, dass wenn das <code>=</code> nicht
    matcht, muss die Engine gar nicht erst versuchen, den vorherigen String zu
    kürzen (der vorher gematchte String waren null oder mehr Leerzeichen, das
    kann niemals ein <code>=</code> sein).</p>

    <p>Also sagt man der Engine, dass sie es gar nicht erst versuchen
    soll:</p>

<pre>[% syntax perl6 %]
m:ratchet:global/ ^^(\w+) \s* \= \s* (\N+)\n$$/
[% endsyntax %]</pre>

    <h3 id="rw-modifier">Regexes können beschreibbar sein</h3>

<pre>[[[syntax perl6]]]
my $str = "12+3";
token op { \+ };
$str ~~ m:rw/(\d+) <op> (\+d)/;
$<op> = '(irgendwas)';    # $str ist jetzt "12(irgendwas)3"
[[[endsyntax]]]</pre>
    
    <p>Mit dem <code>:rw</code>-Modifier kann man die Captures einer Regex
    beschreibbar machen, wie oben demonstriert.</p>

    <p>Dabei ändern sich automatisch auch die anderen Captures,
    insbesondere <code>.from</code> und <code>.to</code> der Captures:</p>

<pre>[[[syntax perl6]]]
my $str = "ab";
$str ~~ m:rw/(a)(b)/;
say $/[1].from;        # 1
$/[0] = 'aa';    
say $/[1].from;        # 2
say ~$/;        # aab
[[[endsyntax]]]</pre>

<!--    <h3>Backtracking verhindern - <code>:ratchet</code></h3>

    <p>
-->


    <h2>Eigene Zeichenklassen, Grammatiken</h2>

    <p>Wenn einem die vorgefertigten Zeichenklassen wie <code>\d</code>
    und <code>\w</code> nicht ausreichen, kann man sich selbst welche
    erzeugen, allerdings mit anderer Notation:</p>

<pre>[[[syntax perl6]]]
m/ <[a..yB..J_]> /
[[[endsyntax]]]</pre>

    <p>Damit werden Zeichen aus dem Bereich "a" bis "y" oder "B" bis "J"
    oder der Unterstrich "_" gefunden.</p>

    <p>Man kann auch Zeichenklassen mit einem Minuszeichen negieren:</p>

<pre>[[[syntax perl6]]]
m/<-[a..z]>/;        # alle Zeichen ausser Kleinbuchstaben
m/<[a..z]-[ij]>/;    # alle Kleinbuchstaben außer i und j    
[[[endsyntax]]]</pre>


    <p>Man kann Zeichklassen und Regexes auch Namen geben:</p>

<pre>[[[syntax perl6]]]
token meine_zeichen     { <[a..zA-U]> };
regex identifier        { <meine_zeichen> \w* };    
[[[endsyntax]]]</pre>

    <p>Dabei werden Regexes wie Funktionen definiert, und werden mit dem
    Schlüsselwort <code>token</code>, <code>rule</code> oder <code>regex</code>
    eingeleitet.</p>

    <p>Innerhalb anderer Regexes kann man dann mit der Notation
    <code>&lt;name&gt;</code> auf die Regex zugreifen (dabei sind keine
    Leerzeichen vor oder nach dem Namen erlaubt).</p>

    <p>Dabei ist <code>token</code> ein <code>regex</code> mit einem
    impliziten <code>:ratchet</code>-Modifier (d.h. es findet kein
    Backtracking innerhalb des Tokens statt), eine <code>rule</code> ist eine
    <code>regex</code> mit implizitem <code>:ratchet :sigspace</code>.</p>

    <h3 id="named_capture">Named Captures</h3>

    <p>Einer Regex einen Namen zu geben und in einer andere Regex zu
    verwenden hat noch einen weiteren Effekt:</p>

<pre>[[[syntax perl6]]]
token integer { \d+ };
my $str = "zahl: 25";
my $match =  $str ~~ m/ ^zahl: \s <integer> /;
if $match {
    say "Die Zahl war: $match<integer>";
}
# Ausgabe: Die Zahl war 25
[[[endsyntax]]]</pre>

    <p>Da die <em>subrule</em>, hier <code>integer</code>, einen Namen
    hat, wird eine sogenannte <em>named capture</em> angelegt. Wenn man
    das Match-Objekt als Hash verwendet, kann man unter dem Namen der
    subrule auf den Teil des Strings zugreifen, auf den die subrule
    gepasst hat.</p>

    <p>Der Inhalt einer <em>named capture</em> ist ebenfalls ein
    Match-Objekt:</p>

<pre>[[[syntax perl6]]]
token integer { \d+ };
my $str = "zahl: 25";
my $match =  $str ~~ m/ ^zahl: \s <integer> /;
if $match {
    my $z = $match{"zahl"};
    say $z.from, " bis ", $z.to;
    say $z.WHAT;
}
# Ausgabe: 
# 7 bis 8
# Match
[[[endsyntax]]]</pre>

    <p>Wenn eine subrule mehrfach verwendet, wird unter dem Namen ein
    Array von Matches erstellt:</p>

<pre>[[[syntax perl6]]]
token integer { \d+ };
token add_op  { \+ | - };

if my $match = "23+42" ~~ m/ <integer> <add_op> <integer>/ {
    say $match<integer>.elems;     # Ausgabe: 2
    say $match<integer>[0];        # Ausgabe: 23
}
[[[endsyntax]]]</pre>

    <p>Wenn man kein Freund von Arrays ist, kann man auch einen anderen
    Namen für den Match angeben:</p>

<pre>[[[syntax perl6]]]
token integer { \d+ };
token add_op  { \+ | - };

if my $match = "23+42" ~~ m/ $<lhs>:=<integer> <add_op> $<rhs>:=<integer>/ {
    say $match<lhs>;        # Ausgabe: 23
    say $match<rhs>;        # Ausgabe: 42
}
[[[endsyntax]]]</pre>


    <h3>Named Captures auslassen</h3>

    <p>Angenommen, man möchte obige Regex so erweitern, dass auch
    Leerzeichen zugelassen werden (aber optional sind), könnte man das so
    machen:</p>

<pre>[[[syntax perl6]]]
token integer { \d+ };
token add_op  { \+ | - };

if "3 + b" ~~ m/<integer> <ws> <add_op> <ws> <integer>/ {
    ...
}
[[[endsyntax]]]</pre>

    <p><code>&lt;ws&gt;</code> ist eine "builtin" regex, und steht für ein
    (kontextsensitiv) optionales Leerzeichen (oder mehrere davon).</p>

    <p>Dabei wird im Match-Objekt eine Liste von Leerzeichen angelegt -
    wenn man aber nur an dem verarbeiteten mathematischen Ausdruck
    interessiert ist, interessieren die Leerzeichen aber nicht. Wenn man
    sie nicht im Match-Objekt haben möchte, kann man vor den Namen ein
    Fragezeichen stellen:</p>

<pre>[[[syntax perl6]]]
token integer { \d+ };
token add_op  { \+ | - };

if "3 + b" ~~ m/<integer> <.ws> <add_op> <.ws> <integer>/ {
    # $/ hat hier keinen <ws>-Eintrag
}
[[[endsyntax]]]</pre>

    <h3>Grammatiken</h3>

    <p>Durch das verschachteln von regexes und tokens kann man ganze
    Grammatiken aufstellen und wie eine regex benutzen. In diesem Fall
    spricht man von <em>rules</em>, also Regeln:</p>

<pre>[[[syntax perl6]]]
token integer       { \d+ };
token add_op        { \+ | - };
token mul_op        { \* | \/ };
rule factor         { <integer> [ <mul_op> <integer> ]* }
rule expression     { <factor>  [ <add_op> <factor>  ]* }
[[[endsyntax]]]</pre>

    <p>Damit kann man Mathematische Ausdrücke wie <code>2 + 3*5 -
    4/23</code> erkennen (und entsprechend verarbeiten), während z.B.
    <code>2 3</code> (also zwei Zahlen, die nicht von einem Operator
    getrennt werden) korrekterweise nicht erkannt werden.</p>


    <h2>Weitere Elemente von Regexes</h2>

    <h3>Konjunktionen</h3>

    <p>Regexes unter Perl 6 können Konjunktionen enthalten, d.h. zwei
    (oder mehr) Regexes, die gemeinsam mit dem selben Start- und Endpunkt
    matchen müssen:</p>

<pre>[[[syntax perl6]]]
if "1 + 3 * 4" ~~ m/^[<expression> & ^<-[4]>+4 ]$/ {
    ...
}
[[[endsyntax]]]</pre>

    <p>Dieses Beispiel passt auf mathematische Ausdrücke, die mit 1
    anfangen, auf 4 enden und dazwischen keine weitere 4 haben.</p>

    <h3>Code in Regexes ausführen</h3>

    <p>Mit der Konstruktion <code>&lt;{...}&gt;</code> kann man Perl-code
    ausführen, dessen Rückgabewert als Regex ausgewertet wird.</p>

    <p>Zum Beispiel kann man damit eine ganze Zahl, gefolgt von ihrer 
    Wurzel, abgerundet auf die nächste ganze Zahl, gematched werden:</p>

<pre>[[[syntax perl6]]]
m/ (\d+) <ws> <{ return $0.sqrt.int; }> /
[[[endsyntax]]]</pre>

    <h3>Lookahead- und Lookbehind-Assertions</h3>

    <p>Mit <code>&lt;before ...&gt;</code> und 
    <code>&lt;after ...&gt;</code>
    kann man vor und hinter eine Regex schauen, der Rest in den spitzen
    Klammern wird als Regex interpretiert.</p>

<pre>[[[syntax perl6]]]
m/ <before foo> \d+ <after bar> /
# ist fast das gleiche wie
m /foo \d+ bar/
[[[endsyntax]]]</pre>

    <p>Der Unterschied ist, dass bei der zweiten Regex <code>foo</code>
    und <code>bar</code> zum Match gehören, bei der ersten nicht.</p>

    <p>Man kann alle Konstruktionen in spitzen Klammern auch mit einem
    Ausrufezeichen negieren:</p>

<pre>[[[syntax perl6]]]
m/ \d+ <!after <sp> Euro>/ 
# passt auf "3 Dollar", aber nicht auf "3 Euro"
[[[endsyntax]]]</pre>

    <h2>Mit Rules parsen</h2>

    <p>Mit Rules kann man sehr leicht Parser schreiben, also Programme,
    die einen Text in eine interne Struktur verarbeiten.</p>

    <p>Als Beispiel sollen einfache Konfigurationsdateien gelesen
    werden:</p>

<pre>[% syntax sh %]
# Das ist ein Kommentar

# Strings werden mit doppelten Anführungszeichen
ip = "127.0.0.1";    

timeout = 120; # Zahlen können einfach so darstehen

# Variablen können ohne Anführungszeichen zugewiesen werden:
other_timeout = timeout; 
[% endsyntax %]</pre>

    <p>Das ist zwar noch leicht ohne Rules zu parsen, aber des
    pädagogischen Wertes wegen soll es mit Rules geparsed werden.</p>

    <p>Ein erster naiver, aber gar nicht so schlechter Ansatz sieht so
    aus:</p>

<pre>[% syntax perl6 %]
grammar config_file {
    rule config        { <config_line>* };
    # Kommentare müssen nicht gespeichert werden:
    rule confg_line    { <?comment> | <assignment> };
    token comment      { '#' \N* \n };
    rule assigment     { <ident> '=' <expression> ';' };
    rule expression    { <number> | <quoted_string> | <ident> };
    rule quoted_string { \" <-["]>* \" };
}
[% endsyntax %]</pre>

    <p>Das parsed eine Konfigurationsdatei, hat aber einen großen
    Nachteil: Wenn die Datei einen Syntaxfehler hat, dann matched die
    Regex nicht - aber es gibt keine Fehlermeldung, was schief gelaufen
    ist.</p>

    <p>Alternative schafft z.B. folgende Konstruktion, hier nur an zwei
    Beispielen gezeigt:</p>

<pre>[% syntax perl6 %]
    rule config_line {
            | <?comment>
            | <assignment>
            | <fail: "Expected comment or assignment">
    };
    rule assigment     { 
        <ident> 
        [ '=' |  <fail: "Expected '='"> ]
        <expression> 
        [ ';' | <fail: "Expected ';'"> ]
    };
[% endsyntax %]</pre>

<!--
    vim: expandtab sw=4 ts=4
-->
