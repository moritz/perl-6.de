[[[menu main tut p5 index]]]
[[[setvar title Tutorial für Perl 5-Programmierer]]]
<div id="content">
    <h1>Perl 6 für Perl 5-Programmierer</h1>

    <p>Trotz vieler Änderungen von p5 zu p6 ist auch vieles gleich
    geblieben, was es alt eingesessenen Perl 5-Programmieren sicher
    leichter macht als anderen, Perl 6 zu lernen.</p>

    <p>Das hier ist ein Versuch, die wichtigsten Änderungen
    zusammenzufassen, die häufigsten am Anfang, danach grob nach
    Kategorien.</p>

    <h2>Verschiedenes</h2>

    <p><code>use strict;</code> ist per default angeschaltet, Ausnahme
    sind Einzeiler die mit <code>perl -e '$code'</code> ausgeführt
    werden.</p>

    <p>Für Methodenaufrufe gibt es jetzt die <code>.</code>-Notation (anstelle
    von <code>-&gt;</code>), die meisten Builtins sind jetzt auch Methoden:</p>

<pre>[[[syntax perl6]]]
my @list;
@list.push("foo");   # wie push @list, "foo"
"foo".print;         # wie print "foo";
[[[endsyntax]]]</pre>
    <p>Die alte Notation ist immer noch unterstützt.</p>
    <p>Wenn man beim Aufruf Methoden (also Routinen, die zu einem Objekt gehören)
    mit Argumenten, die Klammern weglassen will, braucht man einen Doppelpunkt:</p>
 
<pre>[[[syntax perl6]]]
@list.push: 'foo';
[[[endsyntax]]]</pre>

    <p>Die <code>print</code>-Funktion hat eine Schwester, die ein
    Newline am Ende anhängt:<code>say</code> (auch schon in perl 5.10
	vorhanden).</p>

<pre>[[[syntax perl6]]]
my $answer  = 7 * 6;
# alle folgenden Varianten sind äquivalent:
print $answer, "\n";
say $answer;
$answer.say;
[[[endsyntax]]]</pre>

    <h2>Sigils, Arrays und Hashes</h2>

    <p>Wie bisher fangen Skalare mit <code>$</code>, Arrays mit
    <code>@</code> und Hashes mit <code>%</code> an.</p>
        
    <p>Allerdings bleibt bei Arrays und Hashes der Sigil beim Zugriff auf
    Elemente erhalten:</p>
<pre>[[[syntax perl6]]]
my @list = 1, 3, 5, 1, 2; # keine Klammern noetig
my %hash =
    foo => 'bar',
    baz => 'qox',
    ;
@list[0];        #früher: $list[0]
%hash{'foo'};    #früher: $hash{'foo'}
%hash<foo>;      #früher: $hash{foo}
[[[endsyntax]]]</pre>

    <p>Außerdem gibt es auch noch das Sigil <code>&amp;</code> für
    Subroutinen, Closures und Regexes:</p>
<pre>[[[syntax perl6]]]
my $i = 1;
my &func :=  sub { return $i++; }
# und später
say func;
[[[endsyntax]]]</pre>


    <p>Das Autoquoting beim Zugriff auf Hashes funktioniert in
    geschweiften Klammern nicht mehr, statt dessen gibt es jetzt
    <code>&lt;...&gt;</code>:</p>

<pre>[[[syntax perl6]]]
my $str = "foo";
%hash{$str};     # wie bisher
%hash<foo>;      # anstatt %hash{foo}
%hash{'foo'};    # wie bisher
[[[endsyntax]]]</pre>

    <p>Man kann jetzt auch auf Elemente von Array- und Hashreferenzen ohne
    den Pfeil-Operator zugreifen:</p>
<pre>[[[syntax perl6]]]
my $a = [1, 2, [3, 4, 5], 6];
$a[0];       # früher: $a->[0]
$a[2][0];    # früher $a->[0]->[0] oder $a->[0][0]

my $b = { 'a' => 'b', 'c' => [1, 2, 3]};
$b{'a'};     # früher $b->{a}
$b<a>;       # das gleiche
$b<c>[0];    # früher: $b->{c}[0] oder $b->{c}->[0]
[[[endsyntax]]]</pre>

    <p>Die Anzahl der Elemente eines Arrays bekommt man nun mit
	<code>elems</code>:</p>

<pre>[[[syntax perl6]]]
my @list = 1, 2, 4, 5;

my $num = @list.elems;            # früher: scalar @list;
$num = elems @list;               # das gleiche
my $last_index = @list.end;       # früher: $#list
my $last_elem = @list[@list.end]; # früher: $list[$#list]
$last_elem = @list[*-1];          # früher: $list[-1]

# funktioniert immer noch:
if @list < 3 {
	...
}
[[[endsyntax]]]</pre>

    <p><code>qw()</code> wird durch <code>&lt;...&gt;</code> ersetzt und
    hat auch eine interpolierende Form:</p>

<pre>[[[syntax perl6]]]
my @list = <bla blubb>;     # früher: qw(bla blubb)
my $str = "bla blubb";
my @l2 = <<foo $str bar>>;  # liefert ['foo', 'bla', 'blubb', 'bar'];
[[[endsyntax]]]</pre>

    <h2>Twigils</h2>
    <p>Globale Variablen (und ein paar andere) haben ein sekundäres Sigil,
    Twigil genannt:</p>

<pre>[[[syntax perl6]]]
%*ENV               # früher %ENV
@*ARGS              # früher @ARGV
$*IN, $*OUT, $ERR   # früher STDIN, STDOUT, STDERR

# zur Compilezeit bekannte Symbole:
$?LINE          # früher __LINE__
$?FILE          # früher __FILE__
$?PACKAGE       # früher __PACKAGE__

# Implizit deklarierte Variablen haben ein ^ als Twigl:
# in absteigender Reihenfolge sortieren:
sort {$^b <=> $^a}, @list # bisher: sort {$b <=> $a} @list
[[[endsyntax]]]</pre>

    <h2>Kontrollstrukturen</h2>

    <p><code>if</code> und <code>unless</code> benötigen jetzt keine
    Klammern mehr um die Bedingung:</p>

<pre>[[[syntax perl6]]]
if $a < 0 {         # if ($a < 0) {
    say "Negativ";  #   print "Negativ\n";
}                   # }
[[[endsyntax]]]</pre>

    <p>Sollte man trotzdem Klammern verwenden, muss nach dem <code>if</code>
    ein Leerzeichen stehen, sonst wird die Funktion namens <code>if</code>
    aufgerufen (sofern vorhanden).</p>

    <p><code>foreach</code> und <code>for</code> wird zu <code>for</code>, 
	die C-artige <code>for</code>-Schleife zu <code>loop</code>:</p>

<pre>[[[syntax perl6]]]
my @nums = 1, 2, 3, 23, 42;

for @nums -> $i {
    # tolle Berechnung mit $i hier
}

# Iterationsvariablen sind per Default read only.
# Ändern kann man das so:

for @nums -> $i is rw {
    # tolle Berechnung mit $i hier
}
# Alternativ gäbe es noch 'is copy'

# über Hashes iteriern:
for %hash.kv -> my $key, $val {
    say "$key corresponds to $val";
}

# C-Style for-Schleife:
loop (my $i = 0; $i < 10; $i++){
    # tolle Berechnung mit $i hier
}
[[[endsyntax]]]</pre>

    <p><code>while</code> bleibt unverändert (außer dass es auch keine
    runden Klammern mehr um die Bedingung braucht), <code>eval { ...
    }</code> ist jetzt <code>try { ... }</code>.</p>

    <h2>Die Standardvariable <code>$_</code></h2>

    <p><code>for</code>-Schleifen ohne eine explizite Variable benutzen
    auch weiterhin <code>$_</code> als Standard, allerdings wird sie niemals
	von Funktionen wie <code>print</code> als Default verwendet. Dazu braucht 
	man ein <code>.print</code> -- der Punkt zeigt an, dass die Methode mit
    <code>$_</code> als Invocant aufgerufen wird.
    Das gleiche gilt für Funktionen wie
    <code>uc</code> und <code>lc</code>:</p>

<pre>[[[syntax perl6]]]
                 # früher:
for $*IN.lines { # while (<STDIN>){
    .uc.say;     #     print uc;
}                # }
[[[endsyntax]]]</pre>

    <p>Allerdings gibt es auch noch eine andere Möglichkeit: mit dem
    Twigil <code>^</code> kann man Variablen mit beliebigen Namen
    verwenden, und man kann sogar mehrere davon in einem einer Schleife
    verwenden und damit mehrere Variablen auslesen:</p>

<pre>[[[syntax perl6]]]
for 1 .. 4 {
    say $^bla, ": ", $^blubb;
# Ausgabe: 
# 1: 2
# 3: 4
[[[endsyntax]]]</pre>
    <p>Dabei wird den Variablen, die mit <code>$^</code> anfangen, in
    alphabetischer Reihenfolge die Werte zugewiesen:</p>

<pre>[[[syntax perl6]]]
for 1 .. 4 {
    say $^foo, ": ", $^bar;
}
# Ausgabe:
# 2: 1
# 4: 3
[[[endsyntax]]]</pre>

    <h2>Funktionen</h2>

    <p>Funktionen werden immer noch mit <code>sub</code>-Keyword
    deklariert, und können jetzt Parameterdeklarationen (<em>Signatur</em>
    genannt) enthalten wie in
    den meisten anderen Programmiersprachen.</p>

<pre>[[[syntax perl6]]]
sub fakultaet(Int $n){
    return  reduce {$^a * $^b}, 1 .. $n;
    # reduce reduziert Listen mit der angegbenen Funktion
}

# immer noch möglich:
sub fakultaet2 {
    return  reduce {$^a * $^b}, 1 .. @_[0];
}

# Parameter können auch beschreibbare Aliase sein:
sub swap($a is rw, $b is rw){
    $a, $b = $b, $a;
}
[[[endsyntax]]]</pre>

    <h2>Typen und <code>ref</code></h2>

    <p>Perl 6 erlaubt es, Typen für Variablen zu deklarieren, es ist aber
    weiterhin optional. Das funktioniert auch mit selbstdeklarierten
    Typen, also <a href="/tutorials/objektmodell">Klassen</a>.</p>

<pre>[% syntax perl6 %]
my Int $ganzzahl;
my Num $zahl;
my Complex $complexe_zahl;
my Str $string;
my Str @a; 		    # Array mit Strings
my Array of Str @b 	# Array von Arrays mit Strings

class Foo {
    # Defintionen hier
}

my Foo $foo;
[% endsyntax %]</pre>
    
    <p>Die Funktion <code>ref</code> gibt es nicht mehr, dafür gibt es als
    Ersatz zwei Möglichkeiten:</p>

<pre>[[[syntax perl6]]]
my $str = "foo";
say $str.WHAT;		# Ausgabe "Str"

# Ueberprüfung, ob etwas einen bestimmten Typ hat:
say "Das ist ein String" if $str ~~ Str
[[[endsyntax]]]</pre>

    <p>Die zweite Version verwendet den sogenannten <em>Smart Match
    Operator</em> <code>~~</code>, das auf für Regular Expressions
    verwendet wird. Diese Form sollte man verwenden, wenn man überprüft,
    ob etwas einen bestimmten Typ hat, da das auch Vererbung
    berücksichtigt, d.h. sowohl <code>[1, 2] ~~ Array</code> als auch
    <code>[1, 2] ~~ List</code> liefern True zurück.</p>

    <h2>Regexes</h2>

    <p>Perl 5 Regexes können in Perl 6 immer noch verwendet werden, aber
    es gibt auch viele Erweiterungen. Darüber
    gibt es eine eigene Seite: <a href="regex">Regexes und Rules</a>.</p>

    <h2>Strings</h2>

    <p>Strings in Perl 6 sind Unicode-Strings, die auf verschiedenen
    Ebenen gleichzeitig existieren können (bytes, code points,
    graphemes und charlingua).</p>

    <p>Deswegen gibt es kein <code>length</code> mehr, weil nicht 
    defniert ist, was damit gemeint ist. Stattdessen kann mit
    <code>bytes</code> die Anzahl der Bytes abfragen, mit
    <code>chars</code> die Anzal der Zeichen , mit
    <code>codes</code> die Anzahl der Codepoints, mit
    <code>graphs</code> die Anzahl der Grapheme.</p>

    <p>So haben z.B. die deutschen Umlaute in UTF-8 zwei Bytes, aber je
    nur ein char, code und graph.</p>

    <p>Dagegen können z.B. Ligaturen, also aus zwei Buchstaben zusammen
    gesetzte Zeichen, nur ein grapheme aber zweide codepoints haben.</p>

    <p>Wenn man mit <code>split</code> Strings aufspalten will, muss man die
    Regex als <code>rx/.../</code> übergeben:</p>
<pre>[% syntax perl6 %]
"foo bar  baz".split: rx/\s+/;
[% endsyntax %]</pre>

    <p>Zusätzlich gibt es <code>comb</code> (von "to comb", durchkämmen), das alle
    Teile eines Strings zurückliefern, die auf eine regex passen - quasi
    ein invertiertes <code>split</code>.</p>

    <p>Mit <code>index</code> und <code>rindex</code> kann man das erste
    und letzte Vorkommen eines substrings finden:</p>

<pre>[[[syntax perl6]]]
"foo".index("o");   # ist 1
"foo".rindex("o");  # ist 2
[[[endsyntax]]]</pre>

    <p>Strings werden jetzt mit <code>~</code> zusammengefügt (früher:
    <code>.</code>), die Tilde soll zwei Arme symbolisieren, die nach den
    beiden Enden der Strings greifen sollen:</p>

<pre>[[[syntax perl6]]]
my $name = "Perl";
my $version = 6;
my $lang = $name ~ " " ~ $version;
[[[endsyntax]]]</pre>

    <p>Gleichzeitig erzwingt die Tilde Stringkontext:</p>

<pre>[[[syntax perl6]]]
my @a = "foo", "bar;
my $str = ~@a;      # "foo bar"
[[[endsyntax]]]</pre>

    <h2>I/O</h2>

    <p>Aus der <code>&lt;$fh&gt;</code>-Notation zum Lesen aus Filehandles
    ist <code>$fh.lines</code> geworden, und das Zeilenende wird jetzt
    per default automatisch entfernt ("autochomp").</p>

<pre>[% syntax perl6 %]
my $fn = "foo.txt";
my $file = $fn.open err die "Can't read '$fn': $!";

for $file.lines -> $line {
    # hier die Zeile verarbeiten
}

# oder:
my $ganze_datei = $file.slurp;

$file.close;
[% endsyntax %]</pre>

    <p>Einzelne Zeilen liefert <code>$handle.get</code>.</p>

    <p>Die empfohlene Art, eine Datei zeilenweise zu lesen ist jetzt eine
    <code>for</code>-Schleife. Und das bedeutet nicht wie in Perl 5, dass
    erst die gesamte Datei gelesen wird und dann darüber iteriert wird,
    sondern die Werte werden dann aus der Datei gelesen, wenn sie
    gebraucht werden.</p>

    <p>Das ist möglich, weil in Perl 6 Listen per default <em>lazy</em>
    sind, was es auch möglich macht, unendliche Listen zu verwenden:
    <code>(1 .. *)[1]</code>.</p>

    <p>Und so geht das Schreiben in Dateien:</p>

<pre>[[[syntax perl6]]]
my $fn = "foo.txt";
my $file = open($fn, :w) err die "Can't open '$fn' for writing: $!";

$file.say: "Erste Zeile der Datei";
# oder:
say $file: "Zweite Zeile";
print $file: 'Diese Zeile hat kein \n am Ende';
$file.close;
[[[endsyntax]]]</pre>

    <p>Man beachte, dass aus <code>print $fh $text</code> jetzt
    <code>print $fh: $text</code> (mit einem zusätlichen Doppelpunkt)
    geworden ist, da <code>print</code> u.A.eine Methode der zuständigen
    IO-Klasse ist.</p>

    <h2>File Tests</h2>

    <p>File test, wie z.B. <code>-e $filename</code> werden jetzt mit dem
    Smart Match-Operator durchgeführt, und können sowohl auf Strings (als
    Dateinamen) als auch auf Filehandles angewandt werden:</p>

<pre>[% syntax perl6 %]
my $fn = "/etc/passwd";

if $fn ~~ :e {
    say "$fn existiert";
} else {
    say "Wohl kein Unix...";
}
[% endsyntax %]</pre>

    <p>Der <code>:s</code>-Filetest liefert True zurück, wenn die Datei
    nicht leer ist, und im numerischen Kontext gibt er die Länge der Datei
    in Byte zurück.</p>


    <h2>system und exec</h2>

    <p><code>system</code>, also das Ausführen von anderen Programmen,
    heißt jetzt <code>run</code>:</p>

<pre>[[[syntax perl6]]]
my $status = run("mutt -f /path/to/mbox");
# oder
$status = run("mutt", "-f", "/path/to/mbox");
if $status {
    say "Mutt lief erfolgreich";
} else {
    say "Fehler beim ausführen von mutt, Rückgabewert: $status";
}
[[[endsyntax]]]</pre>

    <p>Der Rückgabewert, hier <code>$status</code>, ist im Boolean
    context <code>True</code>, wenn das Programm <code>0</code>
    zurückgeliefert hat, und nicht <code>False</code> wie normale 
    Integer.</p>


    <p><code>exec</code> heißt jetzt <code>runinstead</code>.</p>


    <h2>Neue Features</h2>

    <p>Eine ganze Reihe neuer Features gibt es in Perl 6, die es in Perl 5
    in dieser Form noch gar nicht gab.</p>

    <h3>Metaoperatoren</h3>

    <p>Metaoperatoren verwenden andere Operatoren, um sie z.B. auf Listen
    anzuwenden:</p>

<pre>[[[syntax perl6]]]
my @a = 1 .. 4;
my $sum = [+] @a;       # $sum = 10
# Aequivalent:
$sum = reduce &infix:<+>, @a;
$sum = reduce {$^a + $^b}, @a;
$sum = reduce -> $a, $b { $a + $b }, @a;

# Hyperoperatoren:
my @b = <4 3 2 1>
my @sum = @a »+« @b      # @sum = 5, 5, 5, 5
# kann man auch als >>+<< schreiben

my @b_plus_one = @b »+ 1;
[[[endsyntax]]]</pre>

    <p>Der Hyperoperator <code>»...«</code> wendet den Operator in der
    Mitte paarweise auf die Elemente zweier Listen an. Wenn man auf einer
    Seite keine Liste sondern einen Skalar angibt, muss man die
    entsprechende Hälfte des Hyperoperators weglassen.</p>

    <p>Anstatt der Unicode-Zeichen <code>»</code> und <code>«</code> kann
    man auch doppelte Kleiner- und Größerzeichen
    <code>&gt;&gt;...&lt;&lt;</code> benutzen.</p>

    <p>Hyper- und Reduktionsoperator führen ihre Operationen auf
    Multi-Prozessor-Systemen parallelisiert aus, wenn der Interpreter bzw.
    Compiler entsprechend konfiguriert wird.</p>

    <h2>Verschiedenes (2)</h2>
    <h3><code>wantarray</code> wird <code>want</code></h3>

    <p>Die Funktion <code>wantarray</code> ist dem allgemeineren
    <code>want</code>-Builtin gewichen.</p>

    <p>Die alte Funktionalität bekommt man mit <code>want.list</code> oder
    alternativ <code>want ~~ List</code>.</p>

    <p>Allgemein kann man mit <code>want ~~ $typ</code> abfragen, ob der
    Aufrufer den Typ <code>$typ</code> erwartet, und mit
    <code>want.count</code>. Mehr dazu im Abschnitt <a
    href="/tutorials/kontext">Kontexte</a>.</p>

    <h3>Data::Dumper</h3>
    <p><code>Data::Dumper</code> ist jetzt überflüssig, dafür gibt es die
    eingebaute Methode <code>perl</code>:</p>

<pre>[[[syntax perl6]]]
my %foo = "a" => "b", "c" => <d e f>;
say %foo.perl;
#Ausgabe: {("a" => "b"), ("c" => ("d", "e", "f"))}
[[[endsyntax]]]</pre>

    <p><code>perl</code> gibt Perl-Code aus, der das entsprechende
    Konstrukt erzeugt.</p>

    <h3>Ausführen anderer Programmiersprachen</h3>

    <p>Mit <code>eval</code> können jetzt auch andere Programmiersprachen
    ausgeführt werden, insbesondere Perl 5, aber auch <a
    href="http://www.yaml.org/">Yaml</a> (liefert dann Datenstrukturen
    zurück), und je nach Compiler/Interpreter eventuell Java, Haskell,
    Ruby und andere:</p>

<pre>[[[syntax perl6]]]
eval("/* Java-Code hier, der einen Wert liefert */", :lang(Java));
[[[endsyntax]]]</pre>

    <p>Die Flexibilität der Datenstrukturen, die von der "Fremdsprache"
    zurückgegeben werden können, hängt sicher von der entsprechenden
    Sprache und dem Interface ab, aber zumindest Perl 5 sollte beliebig
    komplexe Datenstrukturen (inklusive Objekte) zurückliefern können.</p>
        
    <h3>Pointy Blocks</h3>

    <p><em>Pointy Blocks</em> sind eine neue Möglichkeit, anonyme
    Codeblöcke zu schreiben:</p>

<pre>[[[syntax perl6]]]
my $square = -> $a { $a * $a };
say $square(4);
# Ausgabe: 16

say $square.WHAT
# Ausgabe: Block
[[[endsyntax]]]</pre>

    <p>Man beachte, dass die <code>for</code>-Schleife, wie sie vorher
    vorgestellt wurde, eine <em>pointy block</em> benutzt:</p>

<pre>[[[syntax perl6]]]
for 1 .. 5 -> $a {
    say $a;
}
[[[endsyntax]]]</pre>

    <p>Alternativ können auch <em>self declaring formal parameters</em>
    verwendet werden, also Variablen mit dem <code>$^</code>-Sigil:</p>

<pre>[% syntax perl6 %]
for 1 .. 5  {
    say $^number;
}
[% endsyntax %]</pre>
